import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    repositories {
        gradlePluginPortal()
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:5.1.0'
        classpath 'gradle.plugin.net.minecrell:licenser:0.4.1'
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'net.minecrell.licenser'

    repositories {
        mavenCentral()
        mavenLocal()
        maven {
            name = 'sonatype'
            url = 'https://oss.sonatype.org/content/groups/public/'
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    dependencies {
        compile 'org.jetbrains:annotations:16.0.3'

        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
    }

    group = project.pluginGroup
    version = project.pluginVersion

    sourceCompatibility = 11
    targetCompatibility = 11

    processResources {
        from(sourceSets.main.resources.srcDirs) {
            filter ReplaceTokens, tokens: [version: version]
        }
    }

    build.dependsOn(shadowJar)

    shadowJar {
        dependencies {
            include(dependency(':debuggery-common'))
        }

        setArchiveClassifier(null)
        preserveFileTimestamps = false
        from rootProject.file("LICENSE.md")
    }

    test {
        testLogging.showStandardStreams = true
        useJUnitPlatform()
    }

    // updateLicenses | checkLicenses
    license {
        ext.name = 'Debuggery'
        header = rootProject.file('HEADER.txt')
    }

    jacoco {
        toolVersion = "0.8.2"
    }

    jacocoTestReport {
        reports {
            xml.enabled = true
            html.enabled = false
        }
    }

    check.dependsOn jacocoTestReport
}
