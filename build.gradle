buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'java'
    id 'jacoco'
    id 'net.minecrell.licenser' version "0.4.1"
}

group = pluginGroup
version = pluginVersion

sourceCompatibility = 11
targetCompatibility = 11

repositories {
    mavenCentral()
    maven {
        name = 'destroystokyo-repo'
        url = 'https://repo.destroystokyo.com/repository/maven-public/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

dependencies {
    compile 'com.destroystokyo.paper:paper-api:1.13.1-R0.1-SNAPSHOT'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
}

import org.apache.tools.ant.filters.ReplaceTokens

processResources {
    from(sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [version: version]
    }
}

jar {
    from "LICENSE.md"
    archivesBaseName = pluginName
}

compileTestJava {
    // Gradle/JUnit/Somebody's ASM lib isn't updated for JEP-181 and it breaks things
    // hopefully less things than this will inevitably break in the future
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

test {
    testLogging.showStandardStreams = true
    useJUnit()
}

// updateLicenses | checkLicenses
license {
    ext.name = 'Debuggery'
    header = project.file('HEADER.txt')
}

jacoco {
    toolVersion = "0.8.2"
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = false
    }
}

check.dependsOn jacocoTestReport
